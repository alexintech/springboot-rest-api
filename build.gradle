import org.springframework.boot.gradle.run.BootRunTask

buildscript {
	ext {
		springBootVersion = '1.5.9.RELEASE'
		springDataInfluxdbVersion = '1.6'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

group = 'com.lsnotes.sandbox'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = 1.8
targetCompatibility = 1.8
mainClassName = 'com.lsnotes.sandbox.springboot.restapi.AppEntry'

// Runs bootRun task with --spring.profiles.active=dev
task bootRunDev(type: BootRunTask, dependsOn: 'build') {
	group = 'Application'
	doFirst() {
		main = project.mainClassName
		classpath = sourceSets.main.runtimeClasspath
		systemProperty 'spring.profiles.active', 'dev'
	}
}

// Copies the project into a folder that'll be copied into the runtime container.
task buildForContainer(type: Copy) {
	dependsOn build
	from jar
	rename {'app.jar'}
	into "${project.projectDir}/build/output"
}

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-devtools')
	compile("com.github.miwurster:spring-data-influxdb:${springDataInfluxdbVersion}")
}
